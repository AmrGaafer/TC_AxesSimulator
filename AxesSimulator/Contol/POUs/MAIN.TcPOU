<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="MAIN" Id="{66959d8b-103b-4309-9846-4d9c01568e87}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MAIN
VAR
	fbTimeout						: TON;
	
	(* GUI Parameters *)
	// User Control
	boShowLoad						: BOOL;
	boShowStart						: BOOL;
	boShowPause						: BOOL;
	boShowStop						: BOOL;
	
	(* Show Functions & Parameters *)
	// 1. csv file loading
	fbCsvRead						: FB_CsvRead_CamTable;
	iFrames							: INT;
	boShowLoadError					: BOOL;
	boShowLoading					: BOOL;
	boShowLoaded					: BOOL;
	i								: INT;
	iTable							: INT	:= 0;
	
	// 2. show manupulation
	iShowState						: INT := 0;
	
	// camming table
	TableCamRef						: ARRAY[0..iMotorsNum-1] OF MC_CAM_REF;
	MC_CamTableSelect				: MC_CamTableSelect;
	MC_ReadCamTableCharacteristics	: ARRAY[0..iMotorsNum-1] OF MC_ReadCamTableCharacteristics;
	MC_TableCharacValues			: ARRAY[0..iMotorsNum-1] OF MC_TableCharacValues;
	
	// 1. Master Axis
	AXMaster						: AXIS_REF;
	fbMasterEnable					: MC_Power;
	fbMasterSetPosition				: MC_SetPosition;
	fbMasterMoveAbs					: MC_MoveAbsolute;
	fbMasterStop					: MC_Stop;
	fbMasterReset					: MC_Reset;
	
	boMasterEnable					: BOOL;
	boMasterSetPosition				: BOOL;
	boMasterMovAbs					: BOOL;
	rMasterTargetPosition			: REAL;
	boMasterStop					: BOOL;
	boShowPauseLatch				: BOOL;
	boShowStopLatch					: BOOL;
	boMasterReset					: BOOL;
	
	// 2. Slave Axes
	AxMotor							: ARRAY[0..iMotorsNum-1] OF AXIS_REF;
	fbEnable						: ARRAY[0..iMotorsNum-1] OF MC_Power;
	fbMoveAbs						: ARRAY[0..iMotorsNum-1] OF MC_MoveAbsolute;
	ST_CamInOptions					: ST_CamInOptions;	// Data Type deceleration — MC_CamIn input
	fbCamIn							: ARRAY[0..iMotorsNum-1] OF MC_CamIn;
	fbCamOut						: ARRAY[0..iMotorsNum-1] OF MC_CamOut;
	fbReset							: ARRAY[0..iMotorsNum-1] OF MC_Reset;
	
	boEnable						: BOOL;
	boEnabled						: BOOL;
	boMoveAbs						: BOOL;
	boMoveAbsDone					: BOOL;
	boMoveAbsError					: BOOL;
	boAxCamIn						: BOOL	:= FALSE;
	boAxCamInDone					: BOOL;
	boAxCamOut						: BOOL	:= FALSE;
	boAxCamOutDone					: BOOL;
	boReset							: BOOL;
	
	boShowActived					: BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// General
boShowLoading										:= fbCsvRead.OUT_boBusy;
boShowLoaded										:= aa2lrTable[iMotorsNum-1][iMaxPoints-1,0] <> 0 AND NOT fbCsvRead.OUT_boBusy;

fbTimeout();

// Logic Control
IF NOT boShowActived THEN
	fbCsvRead(
		IN_smaxDirectoryName		 				:= sFileDir, 
		IN_sFileName								:= sFileName, 
		IN_sColumnSeparator							:= ',', 
		IN_iMotorsNr								:= iMotorsNum, 
		IN_boStartReading							:= boShowLoad, 
		IN_paa2lrTable								:= ADR(aa2lrTable), 
		OUT_boError									=> boShowLoadError);
	IF fbCsvRead.OUT_boDone AND NOT fbCsvRead.OUT_boError THEN
		iFrames										:= fbCsvRead.OUT_iRowsnNumber;
		boShowLoad									:= FALSE;
	ELSIF fbCsvRead.OUT_boDone AND fbCsvRead.OUT_boError THEN
		boShowLoad									:= FALSE;
	END_IF
	IF boShowLoaded AND boShowStart THEN
		boShowActived								:= TRUE;
		iShowState									:= 10;
	END_IF
ELSE
	CASE iShowState OF
		0:		// Idle
			boShowActived							:= FALSE;
			boShowPauseLatch						:= FALSE;
			boShowStopLatch							:= FALSE;
			
		10:		// preparing camming referencing
			rMasterTargetPosition					:= LREAL_TO_REAL(aa2lrTable[0][iMaxPoints-1,0]);	// last time stamp in the table
			FOR i := 0 TO iMotorsNum - 1 DO
				TableCamRef[i].pArray				:= ADR(aa2lrTable[i]);
				TableCamRef[i].ArraySize			:= SIZEOF(aa2lrTable[i]);
				TableCamRef[i].TableType			:= MC_TABLETYPE_NONEQUIDISTANT;
				TableCamRef[i].NoOfRows				:= INT_TO_UDINT(iMaxPoints);
				TableCamRef[i].NoOfColumns			:= 2;
			END_FOR
			iShowState								:= 20;
			
		20:		// select camming table
			MC_CamTableSelect(
				Master								:= AxMaster, 
				Slave								:= AxMotor[iTable], 
				CamTable							:= TableCamRef[iTable], 
				Execute								:= TRUE, 
				Periodic							:= FALSE, 
				MasterAbsolute						:= TRUE, 
				SlaveAbsolute						:= TRUE, 
				CamTableID							:= INT_TO_UDINT(iTable+1), 
				Done=> , 
				Busy=> , 
				Error=> , 
				ErrorID=> );
			IF MC_CamTableSelect.Done AND NOT MC_CamTableSelect.Error THEN
				iTable								:= iTable + 1;
				MC_CamTableSelect(	
					Master							:= AxMaster, 
					Slave							:= AxMotor[iTable], 
					CamTable						:= TableCamRef[iTable],
					Execute							:= FALSE);
				IF iTable = iMotorsNum-1 THEN
					iTable							:= 0;
					iShowState						:= 30;
				ELSE
					//
				END_IF
			ELSIF MC_CamTableSelect.Error THEN
				MC_CamTableSelect(	
					Master							:= AxMaster, 
					Slave							:= AxMotor[iTable], 
					CamTable						:= TableCamRef[iTable],
					Execute							:= FALSE);
				iTable								:= 0;
			END_IF
			
		30:		// kinetic initialization
			fbTimeout.IN							:= TRUE;
			fbTimeout.PT							:= T#5S;
			boMasterEnable							:= TRUE;
			boEnable								:= TRUE;
			boEnabled								:= AXMaster.NcToPlc.StateDWord.0;
			FOR i := 0 TO iMotorsNum - 1 DO
				boEnabled							:= boEnabled AND AxMotor[i].NcToPlc.StateDWord.0;
			END_FOR
			IF boEnabled THEN
				fbTimeout.IN						:= FALSE;
				iShowState							:= 40;
			ELSIF fbTimeout.Q THEN
				fbTimeout.IN						:= FALSE;
				// Error
			END_IF
			
		40:		// Master position reset
			boMasterSetPosition						:= TRUE;
			IF fbMasterSetPosition.Done THEN
				boMasterSetPosition					:= FALSE;
				iShowState							:= 50;
			ELSIF fbMasterSetPosition.Error THEN
				boMasterSetPosition					:= FALSE;
				// Error
			END_IF
			
		50:		// move all axes to the home position
			fbTimeout.IN							:= TRUE;
			fbTimeout.PT							:= T#5S;
			boMoveAbs								:= TRUE;
			boMoveAbsDone							:= TRUE;
			boMoveAbsError							:= FALSE;
			FOR i := 0 TO iMotorsNum - 1 DO
				boMoveAbsDone						:= boMoveAbsDone AND fbMoveAbs[i].Done;
				boMoveAbsError						:= boMoveAbsError OR fbMoveAbs[i].Error;
			END_FOR
			IF boMoveAbsDone AND NOT boMoveAbsError THEN
				fbTimeout.IN						:= FALSE;
				boMoveAbs							:= FALSE;
				iShowState							:= 60;
			ELSIF boMoveAbsError THEN
				fbTimeout.IN						:= FALSE;
				boMoveAbs							:= FALSE;
				// Error
			ELSIF fbTimeout.Q THEN
				fbTimeout.IN						:= FALSE;
				// Error
			END_IF
			
		60:		// couple axes
			fbTimeout.IN							:= TRUE;
			fbTimeout.PT							:= T#5S;
			boAxCamIn								:= TRUE;
			boAxCamInDone							:= TRUE;
			FOR i := 0 TO iMotorsNum - 1 DO
				boAxCamInDone						:= boAxCamInDone AND fbCamIn[i].InSync AND NOT fbCamIn[i].Error;
				IF fbCamIn[i].Error THEN
					// Error
				END_IF
			END_FOR
			IF boAxCamInDone THEN
				fbTimeout.IN						:= FALSE;
				boAxCamIn							:= FALSE;
				iShowState							:= 70;
			ELSIF fbTimeout.Q THEN
				fbTimeout.IN						:= FALSE;
				// Error
			END_IF
			
		70:		// move master axis
			boMasterMovAbs							:= TRUE;
			IF fbMasterMoveAbs.Busy THEN
				boMasterMovAbs						:= FALSE;
				iShowState							:= 75;
			END_IF
			
		75:		// check show end
			IF fbMasterMoveAbs.Done THEN
				iShowState							:= 80;
			END_IF
			IF boShowPause THEN
				boMasterStop						:= TRUE;
				boShowPauseLatch					:= TRUE;
				iShowState							:= 77;
			ELSIF boShowStop THEN
				boMasterStop						:= TRUE;
				boShowStopLatch						:= TRUE;
				iShowState							:= 77;
			END_IF
			
		77:		// stop check
			boMasterStop							:= FALSE;
			IF fbMasterStop.Done THEN
				iShowState							:= 78;
			END_IF
			
		78:		// decision making
			IF boShowPauseLatch AND boShowStart THEN
				boShowPauseLatch					:= FALSE;
				iShowState							:= 70;
			END_IF
			IF boShowStopLatch THEN
				iShowState							:= 80;
			END_IF
			
		80:		// decouple axes
			fbTimeout.IN							:= TRUE;
			fbTimeout.PT							:= T#5S;
			boAxCamOut								:= TRUE;
			boAxCamOutDone							:= TRUE;
			FOR i := 0 TO iMotorsNum - 1 DO
				boAxCamOutDone						:= boAxCamOutDone AND fbCamOut[i].Done AND NOT fbCamOut[i].Error;
				IF fbCamOut[i].Error THEN
					// Error
				END_IF
			END_FOR
			IF boAxCamOutDone THEN
				fbTimeout.IN						:= FALSE;
				boAxCamOut							:= FAlse;
				IF boShowStopLatch THEN
					boShowStopLatch					:= FALSE;
					iShowState						:= 0;
				ELSE
					iShowState						:= 40;
				END_IF
			ELSIF fbTimeout.Q THEN
				fbTimeout.IN						:= FALSE;
				// Error
			END_IF
	END_CASE
END_IF

(* Cyclic Calls *)
// 1. Master Axis
fbMasterEnable(
	Axis											:= AxMaster, 
	Enable											:= boMasterEnable, 
	Enable_Positive									:= TRUE, 
	Enable_Negative									:= TRUE, 
	Override										:= 100, 
	BufferMode										:= MC_Aborting, 
	Options:= , 
	Status=> , 
	Busy=> , 
	Active=> , 
	Error=> , 
	ErrorID=> );

fbMasterSetPosition(
	Axis											:= AxMaster, 
	Execute											:= boMasterSetPosition, 
	Position										:= 0, 
	Mode:= , 
	Options:= , 
	Done=> , 
	Busy=> , 
	Error=> , 
	ErrorID=> );

fbMasterMoveAbs(
	Axis											:= AxMaster, 
	Execute											:= boMasterMovAbs, 
	Position										:= rMasterTargetPosition, 
	Velocity										:= rShowVelocity, 
	Acceleration									:= rShowAcceleration, 
	Deceleration									:= rShowDeceleration, 
	Jerk:= , 
	BufferMode:= , 
	Options:= , 
	Done=> , 
	Busy=> , 
	Active=> , 
	CommandAborted=> , 
	Error=> , 
	ErrorID=> );

fbMasterStop(
	Axis											:= AxMaster, 
	Execute											:= boMasterStop, 
	Deceleration:= , 
	Jerk:= , 
	Options:= , 
	Done=> , 
	Busy=> , 
	Active=> , 
	CommandAborted=> , 
	Error=> , 
	ErrorID=> );

fbMasterReset(
	Axis											:= AxMaster, 
	Execute											:= boMasterReset, 
	Done=> , 
	Busy=> , 
	Error=> , 
	ErrorID=> );

// 2. Pendulum Axis
FOR i := 0 TO iMotorsNum-1 DO
	fbEnable[i](
		Axis										:= AxMotor[i], 
		Enable										:= boEnable, 
		Enable_Positive								:= TRUE, 
		Enable_Negative								:= TRUE, 
		Override:= , 
		BufferMode:= , 
		Options:= , 
		Status=> , 
		Busy=> , 
		Active=> , 
		Error=> , 
		ErrorID=> );
	
	fbMoveAbs[i](
		Axis										:= AxMotor[i], 
		Execute										:= boMoveAbs, 
		Position									:= aa2lrTable[i][0, 1], 
		Velocity									:= lrVelocity, 
		Acceleration								:= lrAcceleration, 
		Deceleration								:= lrDeceleration, 
		Jerk:= , 
		BufferMode:= , 
		Options:= , 
		Done=> , 
		Busy=> , 
		Active=> , 
		CommandAborted=> , 
		Error=> , 
		ErrorID=> );
	
	fbCamIn[i](
		Master										:= AxMaster, 
		Slave										:= AxMotor[i], 
		Execute										:= boAxCamIn, 
		MasterOffset								:= 0, 
		SlaveOffset									:= 0, 
		MasterScaling								:= 1, 
		SlaveScaling								:= 1, 
		StartMode:= , 
		CamTableID									:= 1, 
		BufferMode:= , 
		Options										:= ST_CamInOptions, 
		//InterpolationType							:= MC_InterpolationType_Spline, 
		InSync=> , 
		Busy=> , 
		Active=> , 
		CommandAborted=> , 
		Error=> , 
		ErrorID=> );
	
	fbCamOut[i](
		Slave										:= AxMotor[i], 
		Execute										:= boAxCamOut, 
		Options:= , 
		Done=> , 
		Busy=> , 
		Error=> , 
		ErrorID=> );
	
	fbReset[i](
		Axis										:= AxMotor[i], 
		Execute										:= boReset, 
		Done=> , 
		Busy=> , 
		Error=> , 
		ErrorID=> );
END_FOR
]]></ST>
    </Implementation>
    <LineIds Name="MAIN">
      <LineId Id="1869" Count="2" />
      <LineId Id="2216" Count="0" />
      <LineId Id="1873" Count="17" />
      <LineId Id="2217" Count="1" />
      <LineId Id="2220" Count="0" />
      <LineId Id="2223" Count="0" />
      <LineId Id="1891" Count="2" />
      <LineId Id="2219" Count="0" />
      <LineId Id="1897" Count="0" />
      <LineId Id="2221" Count="0" />
      <LineId Id="1898" Count="110" />
      <LineId Id="2224" Count="0" />
      <LineId Id="2009" Count="16" />
      <LineId Id="2171" Count="1" />
      <LineId Id="2193" Count="0" />
      <LineId Id="2195" Count="0" />
      <LineId Id="2174" Count="1" />
      <LineId Id="2194" Count="0" />
      <LineId Id="2196" Count="0" />
      <LineId Id="2173" Count="0" />
      <LineId Id="2026" Count="0" />
      <LineId Id="2197" Count="0" />
      <LineId Id="2222" Count="0" />
      <LineId Id="2198" Count="0" />
      <LineId Id="2207" Count="0" />
      <LineId Id="2200" Count="1" />
      <LineId Id="2226" Count="0" />
      <LineId Id="2228" Count="5" />
      <LineId Id="2227" Count="0" />
      <LineId Id="2234" Count="0" />
      <LineId Id="2027" Count="12" />
      <LineId Id="2225" Count="0" />
      <LineId Id="2208" Count="0" />
      <LineId Id="2213" Count="0" />
      <LineId Id="2209" Count="0" />
      <LineId Id="2211" Count="1" />
      <LineId Id="2210" Count="0" />
      <LineId Id="2041" Count="49" />
      <LineId Id="2177" Count="11" />
      <LineId Id="2091" Count="0" />
      <LineId Id="2189" Count="0" />
      <LineId Id="2092" Count="77" />
      <LineId Id="246" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>